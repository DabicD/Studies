FUNCTION_BLOCK crashchance

VAR_INPUT
    wiek : REAL;
    moc : REAL;
END_VAR

VAR_OUTPUT
	ryzyko : REAL;
END_VAR

FUZZIFY wiek
	TERM mlody := (20, 1) (22, 1) (30, 0);
	TERM sredni := (22, 0) (30,1) (40,1) (50,0);
	TERM stary := (40, 0) (50, 1) (60, 1);
END_FUZZIFY

FUZZIFY moc
	TERM mala := (20, 1) (80, 1) (100, 0);
	TERM srednia := (80, 0) (110,1) (130,1) (180,0);
	TERM duza := (160, 0) (180, 1) (220, 1);
END_FUZZIFY

DEFUZZIFY ryzyko
	TERM niskie := (0, 1) (4, 1) (6,0) ;
	TERM srednioniskie := (4, 0) (10,1) (12,0);
	TERM srednie := (10, 0) (16, 1) (18, 0);
	TERM sredniowysokie := (16, 0) (22, 1) (24, 0);
	TERM wysokie := (22, 0) (25, 1) (30, 1);

	METHOD : COG;
	DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK No1
	AND : MIN;			// Use 'min' for 'and' (also implicit use 'max' for 'or' to fulfill DeMorgan's Law)
	ACT : MIN;			// Use 'min' activation method
	ACCU : MAX;			// Use 'max' accumulation method

	RULE 1 : IF wiek IS mlody AND moc IS duza THEN ryzyko IS wysokie;
	RULE 2 : IF wiek IS mlody AND moc IS srednia THEN ryzyko IS sredniowysokie;
	RULE 3 : IF wiek IS mlody AND moc IS mala Then ryzyko IS srednie;
	RULE 4 : IF wiek IS sredni AND moc IS duza THEN ryzyko IS sredniowysokie;
	RULE 5 : IF wiek IS sredni AND moc IS srednia THEN ryzyko IS srednie;
	RULE 6 : IF wiek IS sredni AND moc IS mala THEN ryzyko IS niskie;
	RULE 7 : IF wiek IS stary AND moc IS duza THEN ryzyko IS sredniowysokie;
	RULE 8 : IF wiek IS stary AND moc IS srednia THEN ryzyko IS srednie;
	RULE 9 : IF wiek IS stary AND moc IS mala THEN ryzyko IS srednioniskie;

END_RULEBLOCK

END_FUNCTION_BLOCK

